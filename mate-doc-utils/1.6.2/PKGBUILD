pkgname=mate-doc-utils
pkgver=1.6.2
pkgrel=1
pkgdesc='Common files for MATE packages'
arch=('i686' 'x86_64')
url='http://mate-desktop.org'
license=('GPL' 'LGPL')
depends=('libxslt' 'python2' 'docbook-xml' 'rarian')
makedepends=('mate-common' 'automake-1.12')
options=('!emptydirs' '!libtool')
source=("https://github.com/mate-desktop/mate-doc-utils/archive/mate-doc-utils-$pkgver.tar.gz")
sha256sums=('c61adab277f50fa3724e4d946d8d1e366b22e3318a66bfe09f176d73f13e950a')

build() {
    cd "$srcdir/mate-doc-utils-mate-doc-utils-$pkgver"
    AUTOCONF=autoconf AUTOMAKE=automake-1.12 ACLOCAL=aclocal-1.12 PYTHON=/usr/bin/python2 \
        ./autogen.sh --prefix=/usr --sysconfdir=/etc --mandir=/usr/share/man \
        --localstatedir=/var --disable-scrollkeeper || return 1
    make || return 1
}

package() {
    cd "$srcdir/mate-doc-utils-mate-doc-utils-$pkgver"
    make DESTDIR="$pkgdir" install || return 1
    
    # files with conflicts with gnome-doc-utils
    rm -rf -- "$pkgdir/usr/share/xml/mallard/"
    rm -- "$pkgdir/usr/share/pkgconfig/xml2po.pc"
    rm -rf -- "$pkgdir/usr/share/man/"
    rm -rf -- "$pkgdir/usr/lib/"
    rm -- "$pkgdir/usr/bin/xml2po"
}
