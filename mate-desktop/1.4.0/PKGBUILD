pkgname=mate-desktop
pkgver=1.4.0
pkgrel=1
pkgdesc='Library with common API for various MATE modules'
arch=('i686' 'x86_64')
url='http://mate-desktop.org'
license=('GPL' 'LGPL')
depends=('libunique' 'libnotify' 'hicolor-icon-theme' 'gvfs-obexftp' 'obexd-client' 'mate-conf')
makedepends=('mate-doc-utils' 'intltool' 'mate-common' 'gtk-doc')
options=('!emptydirs' '!libtool')
source=("https://github.com/mate-desktop/mate-desktop/archive/mate-desktop-$pkgver.tar.gz")
sha256sums=('77da603aedd67d1358c0551524491d919431dc93ef06420870dea24c75fac05b')


build() {
    cd "$srcdir/mate-desktop-mate-desktop-$pkgver"
    AUTOMAKE=automake AUTOCONF=autoconf ./autogen.sh --prefix=/usr --sysconfdir=/etc --localstatedir=/var \
        --disable-static --disable-scrollkeeper --disable-startup-notification --enable-unique || return 1
    make || return 1
}

package() {
    cd "$srcdir/mate-desktop-mate-desktop-$pkgver"
    make DESTDIR="$pkgdir" install

    # prevent conflicts errors
    mv "$pkgdir/usr/share/omf" "$pkgdir/usr/share/mate-omf"
}

