pkgname=mate-settings-daemon
pkgver=1.4.0
pkgrel=1
pkgdesc='The MATE settings daemon'
arch=('i686' 'x86_64')
url='http://mate-desktop.org'
license=('GPL')
depends=('libmatekbd' 'mate-desktop' 'libnotify' 'gstreamer0.10-base' 'hicolor-icon-theme')
makedepends=('intltool' 'pkgconfig')
options=('!libtool')
install=mate-settings-daemon.install
source=("https://github.com/mate-desktop/mate-settings-daemon/archive/mate-settings-daemon-$pkgver.tar.gz")
sha256sums=('08c67947041e58f401c734077d26c853674b6a1eff7f29d7315b42b37709d5ca')


build() {
    cd "$srcdir/mate-settings-daemon-mate-settings-daemon-$pkgver"
    # remove --disable-pulse if you want pulseaudio support
    AUTOMAKE=automake AUTOCONF=autoconf ./autogen.sh --prefix=/usr --sysconfdir=/etc \
        --localstatedir=/var --libexecdir=/usr/lib/mate-settings-daemon --disable-static \
        --disable-pulse --disable-schemas-install --enable-polkit || return 1
    make || return 1
}

package() {
    cd "$srcdir/mate-settings-daemon-mate-settings-daemon-$pkgver"
    make DESTDIR="$pkgdir/" install
}

