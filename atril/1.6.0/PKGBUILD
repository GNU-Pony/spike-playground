pkgname=atril
pkgver=1.6.0
pkgrel=1
pkgdesc='The MATE document viewer'
arch=('i686' 'x86_64')
url='http://mate-desktop.org'
license=('GPL')
depends=('gtk2' 'libspectre' 'gsfonts' 'poppler-glib' 'djvulibre' 'mate-icon-theme'
         't1lib' 'libmatekeyring' 'desktop-file-utils' 'texlive-bin')
makedepends=('mate-doc-utils' 'caja' 'intltool' 'gobject-introspection' 'gtk-doc' 'automake-1.12')
optdepends=('texlive-bin: DVI support')
install=atril.install
options=('!libtool' '!emptydirs')
source=("https://github.com/mate-desktop/mate-document-viewer/archive/mate-document-viewer-$pkgver.tar.gz")
sha256sums=('8ed41a7b58d0a83b377de269c1a961711a53ff5eddecfc7d0ab7a22961475da4')


build() {
    cd "$srcdir/mate-document-viewer-mate-document-viewer-$pkgver"
    AUTOMAKE=automake-1.12 AUTOCONF=autoconf ./autogen.sh --prefix=/usr --sysconfdir=/etc \
        --localstatedir=/var --libexecdir=/usr/lib/mate-document-viewer --disable-static \
        --enable-{caja,pdf,tiff,djvu,dvi,t1lib,pixbuf,impress,introspection} \
        --disable-scrollkeeper --disable-schemas-{compile,install} || return 1
    make || return 1
}

package() {
    cd "$srcdir/mate-document-viewer-mate-document-viewer-$pkgver"
    make DESTDIR="$pkgdir/" install
    ln -sf atril $pkgdir/usr/bin/mate-document-viewer
}

