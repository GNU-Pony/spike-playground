pkgname=engrampa
pkgver=1.4.0
pkgrel=1
pkgdesc='Archive manipulator for MATE'
arch=('i686' 'x86_64')
url='http://mate-desktop.org'
license=('GPL')
depends=('desktop-file-utils' 'glib2' 'hicolor-icon-theme')
makedepends=('mate-doc-utils' 'intltool')
conflicts=('file-roller' 'file-roller2')
optdepends=('unrar: for RAR uncompression'
            'zip: for ZIP archives'
            'unzip: for ZIP archives'
            'p7zip: 7zip compression utility'
            'arj: for ARJ archives'
            'unace: extraction tool for the proprietary ace archive format')
options=('!libtool' '!emptydirs')
install=engrampa.install
source=("https://github.com/mate-desktop/mate-file-archiver/archive/mate-file-archiver-$pkgver.tar.gz")
sha256sums=('b2ae13de0415e10d92bdcffa3068ea72a9e8a39c064b27cca2dc25c3a11e74cd')


build() {
    cd "$srcdir/mate-file-archiver-mate-file-archiver-$pkgver"
    export AUTOMAKE=automake
    export ACLOCAL=aclocal
    export AUTOCONF=autoconf
    ./autogen.sh --prefix=/usr --disable-static --localstatedir=/var --libexecdir=/usr/lib/engrampa \
        --disable-scrollkeeper --disable-schemas-install --sysconfdir=/etc --disable-packagekit
    mate-doc-prepare --force
    autoreconf -i
    $ACLOCAL
    make
}

package() {
    cd "$srcdir/mate-file-archiver-mate-file-archiver-$pkgver"
    make DESTDIR="$pkgdir/" install
}

