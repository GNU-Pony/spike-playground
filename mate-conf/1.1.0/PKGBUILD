pkgname=mate-conf
pkgver=1.1.0
pkgrel=1
pkgdesc='A configuration database system'
arch=('i686' 'x86_64')
url='http://mate-desktop.org'
license=('LGPL')
depends=('mate-corba' 'libxml2' 'polkit' 'libldap' 'dbus-glib' 'gtk2')
makedepends=('pkgconfig' 'intltool' 'gtk-doc' 'gobject-introspection' 'automake-1.12')
options=('!libtool')
install=mate-conf.install
source=("https://github.com/mate-desktop/mate-conf/archive/mate-conf-$pkgver.tar.gz"
        'mateconf-merge-schema' 'mateconfpkg')
sha256sums=('59a2add60a5fd84735b49989aeec4377e57749191ddc6cceb1200213af393cb6'
            '592bdbc99f6e609e6f47f190a073af6810a39c6919f6d0898ea64cd51a94b1d3'
            '4d2c15038073df8c0eaa139edd78bb7cd66469f52bec9d7c5a73ba4e033b48f6')


build() {
    cd "$srcdir/mate-conf-mate-conf-$pkgver"
    sed -i '1s|#!/usr/bin/env python$|&2|' gsettings/gsettings-schema-convert
    AUTOMAKE=automake-1.12 AUTOCONF=autoconf ./autogen.sh --prefix=/usr --sysconfdir=/etc \
        --libexecdir=/usr/lib/MateConf --localstatedir=/var --disable-static \
        --enable-defaults-service --enable-gsettings-backend=no || return 1
    make || return 1
}

package() {
    cd "$srcdir/mate-conf-mate-conf-$pkgver"
    make DESTDIR="$pkgdir" install
    mkdir -p "$pkgdir/usr/sbin"
    install -m755 "$srcdir/mateconf-merge-schema" "$pkgdir/usr/sbin"
    install -m755 "$srcdir/mateconfpkg" "$pkgdir/usr/sbin"
}

