pkgname=mate-dialogs
pkgver=1.2.0
pkgrel=1
pkgdesc='Display graphical dialog boxes from shell scripts'
arch=(i686 x86_64)
url='http://mate-desktop.org'
license=('LGPL2')
depends=('libnotify' 'gtk2')
makedepends=('intltool' 'gtk-doc' 'mate-doc-utils' 'make' 'mate-common' 'automake' 'gtk2')
provides=('zenity')
conflicts=('zenity')
options=('!emptydirs' '!libtool')
source=("https://github.com/mate-desktop/mate-dialogs/archive/mate-dialogs-$pkgver.tar.gz")
sha256sums=('98e8b54be2272d2764fc102fced67c71cbeb827577932a2d2a9becaa1dc95ef7')


build() {
    cd "$srcdir/mate-dialogs-mate-dialogs-$pkgver"
    AUTOMAKE=automake ACLOCAL=aclocal AUTOCONF=autoconf \
        ./autogen.sh --prefix=/usr --sysconfdir=/etc \
        --localstatedir=/var --disable-static --disable-scrollkeeper || return 1
    make || return 1
}

package() {
    cd "$srcdir/mate-dialogs-mate-dialogs-$pkgver"
    make DESTDIR="$pkgdir" install
    ln -sf -- matedialog "$pkgdir/usr/bin/zenity"
}

